#!/usr/bin/env perl

# Parameters supported:
#
# config
# autoconf
#
# Magic markers:
#%# family=auto
#%# capabilities=autoconf

use strict;
use warnings;
use LWP;
use HTTP::Request::Common;
use JSON qw/decode_json/;

=head1 NAME

elasticsearch_index_total - A munin plugin that collects stats about the index totals

=head1 APPLICABLE SYSTEMS

Elasticsearch

=head1 CONFIGURATION

None

=head1 BUGS

None known so far. If you find any, let me know.

=head1 AUTHOR

Kentaro Yoshida - https://github.com/y-ken
Rowan Wookey - https://www.rwky.net

=head1 LICENSE

MIT

=cut

my $url = exists $ENV{'url'} ? $ENV{'url'} : 'http://localhost:9200';

my $ua = LWP::UserAgent->new;
$ua->timeout(10);

sub autoconf {
    my $req = GET $url, Content_Type => 'application/json';
    if (exists $ENV{'auth_basic_username'} && exists $ENV{'auth_basic_password'}) {
        $req->authorization_basic($ENV{'auth_basic_username'}, $ENV{'auth_basic_password'});
    }
    my $res  = $ua->request( $req );
    my $data = do {
        local $@;
        eval { decode_json( $res->content ) }
    };
    if ( $data->{version}->{number} ) {
        print("yes\n");
    }
    else {
        print("no\n");
    }
    exit(0);
}

if ( $ARGV[0] and $ARGV[0] eq 'autoconf' ) {
    autoconf();
}

sub get_json_from_url {
    my $uri = shift;
    my $req = GET $url . $uri, Content_Type => 'application/json';
    if (exists $ENV{'auth_basic_username'} && exists $ENV{'auth_basic_password'}) {
        $req->authorization_basic($ENV{'auth_basic_username'}, $ENV{'auth_basic_password'});
    }
    my $res  = $ua->request( $req );
    Carp::confess( $res->code . " for " . $uri ) unless $res->is_success;
    my $data = do {
        local $@;
        eval { decode_json( $res->content ) }
    };
    die( "Could not decode JSON from: " . $res->content ) unless $data;
    return $data;
}

my $data   = get_json_from_url("/_nodes");
my $t_data = get_json_from_url("/_nodes/stats");
my %out;

foreach my $full_node_name ( keys %{ $data->{nodes} } ) {
    next unless $t_data->{nodes}{$full_node_name};
    $out{index}   += $t_data->{nodes}{$full_node_name}{indices}{indexing}{index_total};
    $out{get}     += $t_data->{nodes}{$full_node_name}{indices}{get}{total};
    $out{missing} += $t_data->{nodes}{$full_node_name}{indices}{get}{missing_total};
    $out{exists}  += $t_data->{nodes}{$full_node_name}{indices}{get}{exists_total};
    $out{search}  += $t_data->{nodes}{$full_node_name}{indices}{search}{query_total};
    $out{delete}  += $t_data->{nodes}{$full_node_name}{indices}{indexing}{delete_total};
}
if ( $ARGV[0] and $ARGV[0] eq 'config' ) {
    print "graph_title Elasticsearch index operations\n";
    print "graph_category elasticsearch\n";
    print "graph_args --base 1000 -l 0\n";
    print "graph_vlabel Operations per second\n";

    print "graph_order index get missing exists search delete\n";

    for my $name ( keys %out ) {
        print "$name.label $name\n";
        print "$name.type DERIVE\n";
        print "$name.min 0\n";
        print "$name.draw LINE2\n";
    }
}
else {
    foreach my $name ( keys %out ) {
        print "$name.value " . $out{$name} . "\n";
    }
}
